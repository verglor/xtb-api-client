plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
}

group 'com.github.verglor'
version '2.5.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.googlecode.json-simple:json-simple:1.1.1', { transitive = false }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

application {
    mainClass = 'pro.xstore.api.sync.RunExample'
}

jar {
    manifest {
        attributes(
            'Main-Class': application.mainClass.get(),
            'Class-Path': 'lib/json-simple-1.1.1.jar',
        )
    }
    from(configurations.runtimeClasspath.find { it.name == 'json-simple-1.1.1.jar' }) {
        into('lib')
    }
}
